import org.springframework.boot.gradle.tasks.run.BootRun

plugins {
	id 'java'
	id 'groovy'
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version "${dependencyManagementVersion}"
}

group = 'com.olegastakhov'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(22)
		vendor = JvmVendorSpec.AZUL
		implementation = JvmImplementation.VENDOR_SPECIFIC
	}
}

repositories {
	mavenCentral()
}


dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencies {
	implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	/**
	 * Both spring-cloud-loadbalancer and spring-cloud-starter-kubernetes-fabric8
	 * are added because Kubernetes cannot load-balance persistent connections (keepAlive=true).
	 * So, it's up to the "client" to 1) get a list of replicas (in this case, Kubernetes Endpoints
	 * of Services), 2) Open connections 3) load-balance between them 4) refresh connections as needed
	 *
	 * Note that this Gateway has 2 configuration profiles with different routing uris:
	 * - prod-k8s
	 * - <others>
	 * When prod-k8s profile is used, routing uris are LB-based
	 * When <others> profiles are used, routing uris are HTTP-based.
	 *
	 * But the thing is, these dependencies are always present, even for those profiles
	 * which use HTTP-based uris. For HTTP-based uris, there's no need in the following 4
	 * dependencies:
	 * - spring-cloud-loadbalancer
	 * - spring-cloud-starter-kubernetes-fabric8
	 * - spring-boot-starter-cache
	 * - caffeine
 	 */
	implementation("org.springframework.cloud:spring-cloud-loadbalancer")
	implementation 'org.springframework.cloud:spring-cloud-starter-kubernetes-fabric8'
	implementation 'org.springframework.boot:spring-boot-starter-cache' // used by SpringCloudLoadbalancer
	implementation 'com.github.ben-manes.caffeine:caffeine' // used by SpringCloudLoadbalancer
}

configurations {
	all*.exclude module: 'spring-boot-starter-logging'
	all*.exclude module: 'logback-classic'
	all*.exclude group: 'commons-logging', module: 'commons-logging'
}


tasks.withType(JavaExec).configureEach {
	systemProperty 'reactor.schedulers.defaultBoundedElasticOnVirtualThreads', 'true'
}

tasks.withType(BootRun).configureEach {
	systemProperty 'reactor.schedulers.defaultBoundedElasticOnVirtualThreads', 'true'
}

testing {
	suites {
		test {
			useSpock("${spockGroovyVersion}")
			dependencies {
				implementation 'org.apache.groovy:groovy:4.0.22'
				implementation ("org.spockframework:spock-core:${spockGroovyVersion}") {
					exclude group: 'org.apache.groovy'
				}
			}
		}
	}
}


springBoot {
	buildInfo {
		properties {
			// For build-info.properties file under META-INF folder
			group = "${group}"
			name = "${rootProject.name}"
			version = "${version}"
		}
	}
}


bootJar {
	archiveFileName = "${rootProject.name}.jar"
	launchScript()
}
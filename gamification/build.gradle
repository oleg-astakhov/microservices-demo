plugins {
	id 'java'
	id 'groovy'
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version "${dependencyManagementVersion}"
}

group = 'com.olegastakhov'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(22)
		vendor = JvmVendorSpec.AZUL
		implementation = JvmImplementation.VENDOR_SPECIFIC
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// JPA Dependencies, mainly for Liquibase, but can be used for Hybrid DB datasource as well
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'org.postgresql:postgresql'

	// R2DBC Dependencies
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	runtimeOnly 'org.postgresql:r2dbc-postgresql'
	implementation 'io.r2dbc:r2dbc-pool' // Database Connection Pool

	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'org.springframework.boot:spring-boot-starter-amqp' //  RabbitMQ + AMPQ
	implementation "org.liquibase:liquibase-core"

	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'io.micrometer:micrometer-tracing-bridge-brave'
	implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
}


configurations {
	all*.exclude module: 'spring-boot-starter-logging'
	all*.exclude module: 'logback-classic'
	all*.exclude group: 'commons-logging', module: 'commons-logging'
}


tasks.withType(JavaExec) {
	systemProperty 'reactor.schedulers.defaultBoundedElasticOnVirtualThreads', 'true'
}

tasks.withType(org.springframework.boot.gradle.tasks.run.BootRun) {
	systemProperty 'reactor.schedulers.defaultBoundedElasticOnVirtualThreads', 'true'
}

testing {
	suites {
		test {
			useSpock("${spockGroovyVersion}")
			dependencies {
				implementation 'org.apache.groovy:groovy:4.0.22'
				implementation ("org.spockframework:spock-core:${spockGroovyVersion}") {
					exclude group: 'org.apache.groovy'
				}
			}
		}
	}
}

springBoot {
	buildInfo {
		properties {
			// For build-info.properties file under META-INF folder
			group = "${group}"
			name = "${rootProject.name}"
			version = "${version}"
		}
	}
}


bootJar {
	archiveFileName = "${rootProject.name}.jar"
	launchScript()
}